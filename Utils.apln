:Namespace Utils
    ⍝ Utility functions for the code.

    (⎕IO ⎕ML) ← 0 1

    MakeNormalRandomArray ← {
        ⍝ Create array with normally distributed random values.
        ⍝ ⍵: shape of the resulting array.
        ⍝ ⍺: mean and standard deviation of normal distribution used, defaults to (0, 1).              
        ⍝ Returns an array with shape ⍵, with values independently drawn from a normal distribution of parameters set by ⍺.
        ⍝ Uses the Box-Müller Transform, cf. https://en.wikipedia.org/wiki/Box%E2%80%93Muller_transform
        ⍺ ← 0 1
        (mu sd) ← ⍺
        s ← 2○2×○?⍵⍴0
        mu+sd×s×0.5*⍨¯2×⍟?⍵⍴0
    }
    
    Cls2Arr ← {
        ⍝ Turn a class into an array of 0s with a 1 in the component of the class.
        ⍝ ⍵: simple numeric integer vector with class index.
        ⍝ ⍺: simple numeric integer vector with the shape of all outputs.
        ⍝ Returns a sparse array with a single 1.
        ⍺↑⍸⍣¯1,⊂,⍵
    }
    
    Arr2Cls ← {
        ⍝ Given an array of outputs, finds the index of the class with largest value.
        ⍝ ⍵: numeric simple array with network outputs.
        ⍝ Returns a vector that indexes into the largest element of ⍵.
        ⊃⍸⍵=⌈/,⍵
    }
    
    Arr2Clss ← {
        ⍝ Ranks classes according to network outputs.
        ⍝ ⍵: simple numeric array with network outputs.
        ⍝ Returns a vector of ordered classes.
        (⍴⍵)∘⊤¨⍒,⍵
    }
:EndNamespace

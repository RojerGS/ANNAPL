:Namespace ActivationFns
    ⍝ Activation functions for the layers.
    ⍝ Each activation function is a namespace that defines two functions, F and DF,
    ⍝  that implement the activation function itself and its derivative.
    
    ⍝ R ← F Y
    ⍝ Y is an array with values to compute the function on.
    ⍝ R has the same shape as Y, with the corresponding values of the activation functions.
    
    ⍝ R ← DF Y
    ⍝ Y is an array with values to compute the derivative on.
    ⍝ R has the same shape as Y, with the corresponding values of the derivative of the activation function.

    (⎕IO ⎕ML) ← 0 1

    :Namespace ReLU
        ⍝ Rectified Linear Unit
        F ← 0∘⌈
        DF ← 0∘<
    :EndNamespace

    :Namespace LeakyReLU
        ⍝ Leaky Rectified Linear Unit

        ⍝ Parameter that defines the slope of the function for negative values.
        ⍝ Typically set to a positive number less than 1.
        leaky ← 0.01

        F ← {
            (leaky×0⌊⍵)+0⌈⍵
        }

        DF ← {
            (leaky×-×0⌊⍵)+0<⍵
        }
    :EndNamespace

    :Namespace Sigmoid
        ⍝ Sigmoid function

        F ← {       
            ÷1+*-⍵ 
        }

        DF ← {                                                  
            vs×1-vs←F⍵
        }
    :EndNamespace
:EndNamespace

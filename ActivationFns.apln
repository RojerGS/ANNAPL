:Namespace ActivationFns
    ⍝ Activation functions for the layers.
    ⍝ Each activation function is a namespace that defines two functions, F and DF,
    ⍝  that implement the activation function itself and its derivative.
    
    ⍝ R ← F Y
    ⍝ Y is an array with values to compute the function on.
    ⍝ R has the same shape as Y, with the corresponding values of the activation functions.
    
    ⍝ R ← DF Y
    ⍝ Y is an array with values to compute the derivative on.
    ⍝ R has the same shape as Y, with the corresponding values of the derivative of the activation function.

    (⎕IO ⎕ML) ← 0 1
    
    :Namespace Id
        ⍝ Identity function (same as "no activation function")
        F ← ⊢
        DF ← 1⍨¨
    :EndNamespace

    :Namespace ReLU
        ⍝ Rectified Linear Unit
        F ← 0∘⌈
        DF ← 0∘<
    :EndNamespace

    :Namespace LeakyReLU
        ⍝ Leaky Rectified Linear Unit

        ⍝ Parameter that defines the slope of the function for negative values.
        ⍝ Typically set to a positive number less than 1.
        leaky ← 0.01      
        F ← leaky∘(×⌈⊢)    
        DF ← leaky∘(⊣⌈×)
    :EndNamespace
    
    :Namespace ELU
        ⍝ Exponential Linear Unit
         
        ⍝ Multiplicative constant for the negative values.
        alpha ← 0.01
        
        F ← {
            (0⌈⍵)+alpha×¯1+*0⌊⍵
        }                      
        DF ← {
            (0≤⍵)+alpha×(0>⍵)×*⍵
        }
    :EndNamespace

    :Namespace Sigmoid
        ⍝ Sigmoid function

        F ← {       
            ÷1+*-⍵ 
        }   
        DF ← (⊢×1-⊢)∘F
    :EndNamespace
    
    :Namespace Tanh
        ⍝ Hyperbolic Tangent

        F ← 7∘○
        DF ← (1-2*⍨F)
    :EndNamespace
    
    :Namespace ArcTan
        ⍝ Arc Tangent
        
        F ← ¯7∘○
        DF ← (÷1+*∘2)
    :EndNamespace
:EndNamespace

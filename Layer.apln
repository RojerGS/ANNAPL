:Namespace Layer
    ⍝ Fully connected layer of an artificial neural network.
    
    (⎕IO ⎕ML) ← 0 1
    
    ⍝ Parameters of the layer.
    shape ← ⍬                            ⍝ Shape of the layer.
    weights ← ⍬                          ⍝ Tensor with connection weights from previous layer.
    bias ← ⍬                             ⍝ Tensor with bias for this layer.
    ActivationFn ← {⍵}                   ⍝ Activation function to process the output of this layer.
    
    Init ← {
        ⍝ Initialise a layer with given shape.
        ⍝ ⍵: array with shape of the layer.
        ⍝ returns a reference to the namespace itself.
        ⎕THIS.shape ← ⍵
        ⎕THIS.weights ← (0,÷0.5*⍨×/  ⍵) #.Utils.MakeNormalRandomArray   ⍵
        ⎕THIS.bias    ← (0,÷0.5*⍨×/1↓⍵) #.Utils.MakeNormalRandomArray 1↓⍵
        ⎕THIS.ActivationFn ← #.ActivationFns.ReLU
        ⎕THIS
    }
    
    Feed ← {
        ⍝ Feed a vector as input to this layer.
        ⍝ ⍵: array with layer inputs.
        ⍝ returns layer outputs.
        ⎕THIS.ActivationFn ⎕THIS.bias + ⎕THIS.weights+.×⍵
    }
:EndNamespace

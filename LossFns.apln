:Namespace LossFns
    ⍝ Namespace for the loss functions of an artificial neural network.
    ⍝ Each loss function is a namespace that defines two functions, Loss and DLoss.
    ⍝ The first computes the loss of the network and the second is the derivative
    ⍝  of the loss function w.r.t. to the network output.
    ⍝ Both functions expect the array with the final output of the network on the right
    ⍝  and the expected output on the left.

    (⎕IO ⎕ML) ← 0 1

    :Namespace MSELoss
        ⍝ Mean Squared Error loss function.
        
        Loss ← {
            ⍝ ⍵: array with the network output.
            ⍝ ⍺: array with the expected output, same shape as ⍵.
            ⍝ Returns a numeric scalar representing the loss.
            d ← 2*⍨,⍵-⍺
            (+/d)÷≢d
        }           
        
        DLoss ← {
            ⍝ ⍵: array with the network output.
            ⍝ ⍺: array with the expected output, same shape as ⍵.
            ⍝ Returns an array of derivatives with the shape of ⍵.
            n ← ×/⍴⍵
            n÷⍨2×⍵-⍺
        }
    :EndNamespace
:EndNamespace

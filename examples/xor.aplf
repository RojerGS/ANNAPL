∇ net ← xor
    ;actFn ;layer1 ;layer2
    ;refins ;in ;refouts ;out
    ;R ;PP
    ⍝ Example usage of the ANN code.
    ⍝ Creates a network that computes the xor binary function.

    (⎕IO ⎕ML)←0 1

    actFn ← ⎕NS #.ActivationFns.LeakyReLU
    actFn.leaky ← 0.1

    layer1 ← #.Layer.Init 2 3
    layer1.ActivationFn ← ⎕NS actFn
    layer2 ← #.Layer.Init 3 1
    layer2.ActivationFn ← ⎕NS actFn

    net ← ⎕NS #.ANN
    net.layers ← layer1 layer2
    net.LossFn ← #.LossFns.MSELoss
    net.epsilon ← 0.1

    R ← (⌊0.5+⊢) ⍝ Round to nearest integer.
    PP ← { ⍝ Print performance of the net.
        ⎕← 'A B out'
        ⎕← (↑refins) , outs ← R∘⊃∘⌽∘net.Feed∘⍪¨ refins
        ⎕← 'Accuracy is ', 25×+/refouts=outs
    }
    refins ← ↓⍉2 2⊤⍳4   ⍝ Reference inputs and outputs.
    refouts ← ≠/↑refins
    PP net

    ⍞← 'Training... '
    :For _ :In ⍳300
        :For in :In ⍳4
            in ← ⍪2 2⊤in
            out ← ⍪≠⌿in
            ⍝⎕← out net.Loss.Loss ⊃⌽net.Feed in
            _ ← out net.Train in
        :EndFor
    :EndFor
    ⍞← '(done)'

    PP net
∇

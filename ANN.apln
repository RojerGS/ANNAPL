:Namespace ANN
    ⍝ Hold the information needed for an artificial neural network.
    
    (⎕IO ⎕ML) ← 0 1
    
    ⍝ Learning rate parameter
    lr ← 0.01
    ⍝ Vector of namespaces holding the layers of the network.
    layers ← 0⍴#
    ⍝ Loss function namespace of the network.
    LossFn ← ⎕NS''
    
    Feed ← {
        ⍝ Compute the output of the network on an input vector.
        ⍝ ⍵: network input array.
        ⍝ returns an array with the output of the network.
        ⊃{⍵,⊂⍺.Feed⊃⌽⍵}/ (⊂⊂⍵),⍨⌽layers
    }
    
    BackProp ← {
        ⍝ Compute the derivatives of the network parameters w.r.t. some network output.
        ⍝ ⍵: consecutive layer outputs.
        ⍝ ⍺: expected network output.
        dWs ← ⍬
        dbs ← ⍬             
        xs ← 1↓⌽⍵
        dxs ← ⊂⍺ LossFn.DLoss⊃⌽⍵
        BP ← {                                             
            ⍝ Auxiliary function.
            ⍝ The layers and xs are in the reverse natural order of the network,
            ⍝ while the derivative arrays are in the natural order.
            (layers xs dWs dbs dxs) ← ⍵
            l ← ⊃layers
            x ← l.bias+l.weights+.×⊃xs
            db ← (⊃⌽dxs)×l.ActivationFn.DF x
            dx ← (⍉l.weights)+.×db
            dW ← db+.×⍉⊃xs
            (1↓layers) (1↓xs) (dWs,⊂dW) (dbs,⊂db) (dxs,⊂dx)
        }
        r ← BP⍣(≢layers) ⊢ (⌽layers) xs dWs dbs dxs
        (_ _ dWs dbs _) ← r
        (⌽dWs) (⌽dbs)
    }
    
    Train ← {
        ⍝ Train the network on a given input/output sample.
        ⍝ ⍵: network input.
        ⍝ ⍺: expected network output.
        ⍝ Returns a reference to the network itself.
        xs ← Feed ⍵
        ds ← ⍺ BackProp xs              
        T ← {
            (dWs dbs) ← ⍵
            (⊃⎕THIS.layers).weights -← lr×⊃dWs
            (⊃⎕THIS.layers).bias -← lr×⊃dbs
            ⎕THIS.layers ← 1⌽⎕THIS.layers
            (1⌽dWs) (1⌽dbs)
        }
        _ ← T⍣(≢layers) ⊢ ds
        ⎕THIS
    }
:EndNamespace          


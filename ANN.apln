:Namespace ANN
    ⍝ Hold the information needed for an artificial neural network.
    
    (⎕IO ⎕ML) ← 0 1
    
    ⍝ Vector of namespaces holding the layers of the network.
    layers ← 0⍴#
    ⍝ Loss function namespace of the network.
    Loss ← ⎕NS''
    
    Feed ← {
        ⍝ Compute the output of the network on an input vector.
        ⍝ ⍵: network input array.
        ⍝ returns an array with the output of the network.
        ⊃{⍵,⊂⍺.Feed⊃⌽⍵}/ (⊂⊂⍵),⍨⌽⎕THIS.layers
    }
    
    BackProp ← {
        ⍝ Compute the derivatives of the network parameters w.r.t. some network output.
        ⍝ ⍵: consecutive layer outputs.
        ⍝ ⍺: expected network output.
        dWs ← ⍬
        dbs ← ⍬             
        xs ← 1↓⌽⍵
        dxs ← ⊂⍺ Loss.DLoss⊃⌽⍵
        BP ← {                                             
            ⍝ Auxiliary function.
            ⍝ The layers and xs are in the reverse natural order of the network,
            ⍝ while the derivative arrays are in the natural order.
            (layers xs dWs dbs dxs) ← ⍵
            l ← ⊃layers
            x ← l.bias+l.weights+.×⊃xs
            db ← (⊃⌽dxs)×l.ActivationFn.DF x
            dx ← (⍉l.weights)+.×db
            dW ← db+.×⍉⊃xs
            (1↓layers) (1↓xs) (dWs,⊂dW) (dbs,⊂db) (dxs,⊂dx)
        }
        r ← BP⍣(≢⎕THIS.layers) ⊢ (⌽⎕THIS.layers) xs dWs dbs dxs
        (_ _ dWs dbs _) ← r
        (⌽dWs) (⌽dbs)
    }
:EndNamespace          

